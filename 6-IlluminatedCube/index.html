<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <title>Illuminated Cube</title>
</head>
<body>
  <canvas id="canvas" width="640" height="480"></canvas>

  <script id="shader-vs" type="x-shader/x-vertex">
    attribute vec4 a_Pos;
    attribute vec3 a_Normal; 

    uniform mat4 u_PMatrix;
    uniform mat4 u_MVMatrix;
    uniform mat3 u_NMatrix;

    uniform vec3 u_LightPos;

    uniform vec3 u_AmbientMaterialColor;
    uniform vec3 u_DiffuseMaterialColor;
    uniform vec3 u_SpecularMaterialColor;

    uniform vec3 u_AmbientLightColor;
    uniform vec3 u_DiffuseLightColor;
    uniform vec3 u_SpecularLightColor;

    const float c_Shininess = 16.0;
    varying mediump vec4 v_Color;

    vec3 getTransformedPos();
    vec4 getColor(float, float);

    void main() {
      gl_Position = u_PMatrix * u_MVMatrix * a_Pos;
      
      vec3 tPos = getTransformedPos();
      vec3 tNormal = normalize(u_NMatrix * a_Normal);

      vec3 lightDir = normalize(u_LightPos - tPos);
      vec3 reflDir = normalize(reflect(-lightDir, tNormal));
      vec3 viewDir = -normalize(tPos);

      float diffuseDot = max(dot(tNormal, lightDir), 0.0);

      float specularDot = max(dot(reflDir, viewDir), 0.0);
      specularDot = pow(specularDot, c_Shininess);

      v_Color = getColor(diffuseDot, specularDot);
    }

    vec3 getTransformedPos() {
      vec4 pos = u_MVMatrix * a_Pos;
      return pos.xyz / pos.w; 
    }

    vec4 getColor(float diffuseDot, float specularDot) {
      vec3 ambientRefl = u_AmbientMaterialColor * u_AmbientLightColor;
      vec3 diffuseRefl = u_DiffuseMaterialColor * u_DiffuseLightColor * diffuseDot;
      vec3 specularRefl = u_SpecularMaterialColor * u_SpecularLightColor * specularDot;
      return vec4(ambientRefl + diffuseRefl + specularRefl, 1.0);
    }
  </script>

  <script id="shader-fs" type="x-shader/x-fragment">
    varying mediump vec4 v_Color;

    void main() {
      gl_FragColor = v_Color;
    }
  </script>

  <script src="../utils.js"></script>
  <script src="../gl-utils.js"></script>
  <script src="../gl-matrix.js"></script>
  <script src="main.js"></script>
</body>
</html>